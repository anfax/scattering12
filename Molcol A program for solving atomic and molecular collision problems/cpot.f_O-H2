      SUBROUTINE CPOT(NNQ12,NPTPOT,RPOT,VPOT,CM,R,V,NDBPOT,ENEFF,ENSTB)
C   CPOT IS CALLED FROM DVGLR
C   CALCULATES THE POTENTIAL ENERGY CURVES AT THE LOCAL VALUE OF
C   THE INTERMOLECULAR DISTANCE R USING CUBIC SPLINE INTERPOLATION
C   OF THE TABULATED POINTS
C   THE POTENTIAL IS FORMALLY DECOMPOSED INTO HARTREE-FOCK AND
C   DISPERSION CONTRIBUTIONS; IN THE CURRENT VERSION, THE LATTER
C   IS COMMENTED OUT AND THE "HARTREE-FOCK" CONTRIBUTION IS
C   TAKEN EQUAL TO THE TOTAL INTERACTION ENERGY
      IMPLICIT REAL*8(A-H,O-Z)
      COMMON/INTGR/ RDB,RFN,FPT,ENFDML,RMIN,STBMX,FLLMX,ENMIN,NSTBMX
      DIMENSION V(1),NDBPOT(1),NPTPOT(1),RPOT(1),VPOT(1),CM(1)
C***THE FOLLOWING LIMIT IS IMPLIED BY THE FOLLOWING DIMENSION OF VHF:***
C   NNQ12.LE.60
      DIMENSION VHF(60), RE(5)
C      COMMON/DISP/ XDSP(200),CDSP(200),CMDSP(200),NPTDSP(25),NDBDSP(25)
      COMMON/MAS/RDMAS
      DATA RE/15.0D0,15.0D0,15.0D0,15.7D0,15.7D0/
C   CHECK IF DIMENSION EXCEEDED
      IF(NNQ12.GT.60) GO TO 50
      DRDMAS = 2*RDMAS
      R2 = R*R
      R4 = R2*R2
      R5 = R4*R
      R6 = R5*R
      R7 = R6*R
      RM2 = -1./R2
      DO 20 IQ=1,NNQ12
         NDB = NDBPOT(IQ)
         NPNT = NPTPOT(IQ)
C     NDBD = NDBDSP(IQ)
C     NPNTD = NPTDSP(IQ)
C     VDSP(IQ) = SPL(NPNTD,XDSP(NDBD),CDSP(NDBD),CMDSP(NDBD),RM2)
C   BEYOND R = RE, USE ASYMPTOTIC FORMS BELOW
         IF(R.GT.RE(IQ)) GO TO 20
         VHF(IQ) = SPL(NPNT,RPOT(NDB),VPOT(NDB),CM(NDB),R)
 20   CONTINUE
      DO 40 IQ=1,NNQ12
         IF(R.LE.RE(IQ)) GO TO 40
         RF = 1.D-6*(RE(IQ)/R)**6.D0
         IF(IQ.EQ.1) VHF(1) = 0.95D0*RF
         IF(IQ.EQ.2) VHF(2) = -2.0D0*RF
         IF(IQ.EQ.3) VHF(3) = -1.05D0*RF
         IF(IQ.EQ.4) VHF(4) = -3.83D0*RF
         IF(IQ.EQ.5) VHF(5) = 0.75D0*RF
 40   CONTINUE
C
      V(1) = (2.D0*(VHF(1)+VHF(2)+VHF(3)+VHF(5)) + VHF(4))/9.D0
      V(1) = V(1)*DRDMAS
      V(2) = (-VHF(1)-VHF(2)-VHF(3)+VHF(4)+2.D0*VHF(5))*0.0993808D0
      V(2) = V(2)*DRDMAS
      V(3) = (2.D0*(VHF(4)-VHF(5)-VHF(1)) + VHF(2) + VHF(3))/9.D0
      V(3) = V(3)*DRDMAS
      V(4) = (VHF(4)-VHF(5)-VHF(2)-VHF(3)+2.D0*VHF(1))*0.248452D0
      V(4) = V(4)*DRDMAS
      V(5) = (VHF(2) - VHF(3))*0.471405D0
      V(5) = V(5)*DRDMAS
C
C    ENSTB REQUIRED FOR STABILIZATION OF THE SOLUTIONS TO THE
C    DIFFERENTIAL EQUATIONS IN THE CLASSICALLY FORBIDDEN REGION,
C    WHERE THE SUM OF THE INTERACTION AND CENTRIFUGAL POTENTIALS
C    EXCEEDS THE RELATIVE COLLISION ENERGY.
C    CHOOSE AN APPROACH WHICH GIVES RISE TO THE MOST REPULSIVE
C    SHORT RANGE AND ATTRACTIVE LONG RANGE INTERACTION. IN THIS EXAMPLE,
C    THETA = 0 IS CHOSEN (THETA IS THE ANGLE BETWEEN THE INTER-
C    AND INTRA-MOLECULAR VECTORS)
      ENSTB = ENMIN - FLLMX/R2 -V(1)
C    ENEFF IS USED IN THE CALCULATION OF THE INTEGRATION STEP SIZE
      ENEFF = ENFDML -V(1)
      RETURN
   50 WRITE(6, 51) NNQ12
   51 FORMAT(1X,'****ATTENTION: NNQ12=',I5,
     1     ' GREATER THAN 60: REDIMENSION VHF IN SUBROUTINE CPOT')
      STOP
      END

